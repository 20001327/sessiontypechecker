%header {:
	/* This code is inlined before the generated parser */
	package miniErlang;
:};

%embed {:
	/* This code is inlined in the generated parser class */
:};

/* Just to shut up Beaver warnings */
%goal goal;

/* Productions with semantic actions building the JastAdd AST */

program goal =
	module_list 	    	    {: return new Program(module_list); :}
;

Module module =
    LBRACKET actor COMMA function_list RBRACKET {: return new Module(function_list); :}
;

Actor actor =
   LCURLYBRACE ATTRIBUTE COMMA INTEGER.rowNumber COMMA MODULE COMMA IDENTIFIER.value RCURLYBRACE
   {: return new Actor(value); :}
;

List module_list =
    module                  {: return new List().add(module); :}
|   module_list module      {: return module_list.add(module); :}
;

List function_list =
	function		        {: return new List().add(function);	:}
|	function_list function	{: return function_list.add(function); :}
;



Function function =
type? LCURLYBRACE FUNCTION COMMA INTEGER.rowNumber COMMA IDENTIFIER.funName COMMA INTEGER.numArguments COMMA clause_list.patterns RCURLYBRACE
     {: return new Function(funName, Integer.parseInt(numArguments), patterns); :}
;

Type type =
   LCURLYBRACE ATTRIBUTE COMMA INTEGER.rowNumber COMMA TYPE COMMA LCURLYBRACE IDENTIFIER.funName COMMA atom COMMA LBRACKET RBRACKET RCURLYBRACE RCURLYBRACE
   {: return new Type(atom.getAtomValue()); :}
 ;


Form form = match
|   LCURLYBRACE OPERATOR COMMA INTEGER.rowNumber COMMA SINGLEMARKS SEND SINGLEMARKS COMMA value.dest COMMA tuple.message RCURLYBRACE
        {: return new Send(dest, message); :}
|   LCURLYBRACE OPERATOR COMMA INTEGER.rowNumber COMMA SINGLEMARKS SYMBOL.sym SINGLEMARKS COMMA value.op1 COMMA value.op2 RCURLYBRACE
        {: return new Operator(sym,op1, op2); :}
|	LCURLYBRACE SINGLEMARKS RECEIVE SINGLEMARKS COMMA INTEGER.rowNumber COMMA clause_list.clauses RCURLYBRACE
 		{: return new Receive(clauses); :}
|   LCURLYBRACE CASE COMMA INTEGER.rowNumber COMMA form.matchingForm COMMA clause_list.clauses RCURLYBRACE
        {: return new Case(matchingForm, clauses); :}
|   LCURLYBRACE CALL COMMA INTEGER.rowNumber COMMA atom.method COMMA value_list.arguments RCURLYBRACE
        {: return new Call(method, arguments); :}
;

List form_list =
    form                    {: return new List().add(form);     :}
|   form_list form          {: return form_list.add(form);      :}
;

List match_list =
    LBRACKET RBRACKET       {: return new List(); :}
|	match   		        {: return new List().add(match);	:}
|	match_list match    	{: return match_list.add(match); :}
 ;

List clause_list =
	clause   		        {: return new List().add(clause);	:}
|	clause_list clause    	{: return clause_list.add(clause); :}
 ;

Match match = LCURLYBRACE MATCH COMMA INTEGER.rowNumber COMMA variable.left COMMA value.right RCURLYBRACE
        {: return new Match(left, right); :}
;

Clause clause = LCURLYBRACE CLAUSE COMMA INTEGER.rowNumber COMMA variable_list.variables COMMA match_list.matches COMMA form_list RCURLYBRACE
       {: return new Clause(variables, matches, form_list); :}
;

Tuple tuple =
 LCURLYBRACE TUPLE COMMA INTEGER.rowNumber COMMA value_list RCURLYBRACE
        {: return new Tuple(value_list); :}
;

List value_list =
        LBRACKET RBRACKET       {: return new List(); :}
|   value VALUE                 {: return new List().add(value); :}
|   value_list value VALUE      {: return value_list.add(value); :}
;

Value value =
    ident
|   atom
|   number
|   charlist
;

Ident ident =
|   variable
|   pid
;

List variable_list =
    LBRACKET RBRACKET       {: return new List(); :}
|   variable                {: return new List().add(variable); :}
|   variable_list variable  {: return variable_list.add(variable); :}
;

Number number =
    LCURLYBRACE INTTYPE COMMA INTEGER.rowNumber COMMA INTEGER.val RCURLYBRACE
    {: return new Number(Integer.parseInt(val)); :}
;

CharList charlist = LCURLYBRACE STRING COMMA INTEGER.rowNumber COMMA STRINGVALUE RCURLYBRACE
      {: return new CharList(STRINGVALUE); :}
;

Atom atom = LCURLYBRACE ATOM COMMA INTEGER.rowNumber COMMA ATOMID.ident RCURLYBRACE
        {: return new Atom(ident); :}
;

Pid pid = PID.mypid
     {: return new Pid(mypid); :}
;

Variable variable =
    LCURLYBRACE VAR COMMA INTEGER.rowNumber COMMA VARIABLEID RCURLYBRACE
    {: return new Variable(VARIABLEID); :}
;
