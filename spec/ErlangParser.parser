%header {:
	/* This code is inlined before the generated parser */
	package miniErlang;
:};

%embed {:
	/* This code is inlined in the generated parser class */
:};

/* Just to shut up Beaver warnings */
%goal goal;
%left RBRACKET;
/* Productions with semantic actions building the JastAdd AST */

program goal =
	module_list 	    	    {: return new Program(module_list); :}
;

Module module =
    LBRACKET actor COMMA function_list RBRACKET {: return new Module(actor.getActorName(), function_list); :}
;

Actor actor =
   LCURLYBRACE ATTRIBUTE COMMA INTEGER.rowNumber COMMA MODULE COMMA IDENTIFIER.value RCURLYBRACE
   {: return new Actor(value); :}
;

List module_list =
    module                  {: return new List().add(module); :}
|   module_list COMMA module      {: return module_list.add(module); :}
;

List function_list =
	function		        {: return new List().add(function);	:}
|	function_list COMMA function	{: return function_list.add(function); :}
;



Function function =
type? LCURLYBRACE FUNCTION COMMA INTEGER.rowNumber COMMA IDENTIFIER.funName COMMA INTEGER.numArguments COMMA LBRACKET clause_list.patterns RBRACKET RCURLYBRACE
     {: return new Function(funName, Integer.parseInt(numArguments), patterns); :}
;

Type type =
   LCURLYBRACE ATTRIBUTE COMMA INTEGER.rowNumber COMMA TYPE COMMA LCURLYBRACE IDENTIFIER.funName COMMA atom COMMA LBRACKET RBRACKET RCURLYBRACE RCURLYBRACE COMMA
   {: return new Type(atom.getAtomValue()); :}
 ;


Form form =
   LCURLYBRACE OPERATOR COMMA INTEGER.rowNumber COMMA SINGLEMARKS SEND SINGLEMARKS COMMA value.dest COMMA tuple.message RCURLYBRACE
        {: return new Send(dest, message); :}
|   LCURLYBRACE OPERATOR COMMA INTEGER.rowNumber COMMA SINGLEMARKS SYMBOL.sym SINGLEMARKS COMMA value.op1 COMMA value.op2 RCURLYBRACE
        {: return new Operator(sym,op1, op2); :}
|	LCURLYBRACE RECEIVE COMMA INTEGER.rowNumber COMMA LBRACKET clause_list.clauses RBRACKET RCURLYBRACE
 		{: return new Receive(clauses); :}
|   LCURLYBRACE CASE COMMA INTEGER.rowNumber COMMA form.matchingForm COMMA LBRACKET clause_list.clauses RBRACKET RCURLYBRACE
        {: return new Case(matchingForm, clauses); :}
|   LCURLYBRACE CALL COMMA INTEGER.rowNumber COMMA atom.method COMMA LBRACKET value_list.arguments RBRACKET RCURLYBRACE
        {: return new Call(method, arguments); :}
|   match
|   value
;

List form_list =
    form                    {: return new List().add(form);     :}
|   form_list COMMA form          {: return form_list.add(form);      :}
;

List match_list =
	match   		        {: return new List().add(match);	:}
|	match_list COMMA match    	{: return match_list.add(match); :}
 ;

List clause_list =
	clause   		        {: return new List().add(clause);	:}
|	clause_list COMMA clause    	{: return clause_list.add(clause); :}
 ;

Match match = LCURLYBRACE MATCH COMMA INTEGER.rowNumber COMMA variable.left COMMA form.right RCURLYBRACE
        {: return new Match(left, right); :}
;

Clause clause = LCURLYBRACE CLAUSE COMMA INTEGER.rowNumber COMMA LBRACKET value_list.patterns RBRACKET COMMA LBRACKET match_list.matches? RBRACKET COMMA LBRACKET form_list RBRACKET RCURLYBRACE
       {: return new Clause(patterns, matches, form_list); :}
| LCURLYBRACE CLAUSE COMMA INTEGER.rowNumber COMMA LBRACKET tuple_list.patterns RBRACKET COMMA LBRACKET match_list.matches? RBRACKET COMMA LBRACKET form_list RBRACKET RCURLYBRACE
       {: return new Clause(patterns, matches, form_list); :}
;


List tuple_list =
   tuple                 {: return new List().add(tuple); :}
|  tuple_list COMMA tuple     {: return tuple_list.add(tuple); :}
;

Tuple tuple =
 LCURLYBRACE TUPLE COMMA INTEGER.rowNumber COMMA LBRACKET value_list RBRACKET RCURLYBRACE
        {: return new Tuple(value_list); :}
;


List value_list =
   value                 {: return new List().add(value); :}
|  value_list COMMA value     {: return value_list.add(value); :}
;

Value value =
    ident
|   atom
|   number
|   charlist
;

Ident ident =
|   variable
|   pid
;

Number number =
    LCURLYBRACE INTTYPE COMMA INTEGER.rowNumber COMMA INTEGER.val RCURLYBRACE
    {: return new Number(Integer.parseInt(val)); :}
;

CharList charlist = LCURLYBRACE STRING COMMA INTEGER.rowNumber COMMA STRINGVALUE RCURLYBRACE
      {: return new CharList(STRINGVALUE); :}
;

Atom atom =
LCURLYBRACE ATOM COMMA INTEGER.rowNumber COMMA IDENTIFIER.i RCURLYBRACE
        {: return new Atom(i); :}
|LCURLYBRACE ATOM COMMA INTEGER.rowNumber COMMA ATOMID RCURLYBRACE
        {: return new Atom(ATOMID); :}
;

Pid pid = PID.mypid
     {: return new Pid(mypid); :}
;

Variable variable =
    LCURLYBRACE VAR COMMA INTEGER.rowNumber COMMA VARIABLEID RCURLYBRACE
    {: return new Variable(VARIABLEID); :}
;
