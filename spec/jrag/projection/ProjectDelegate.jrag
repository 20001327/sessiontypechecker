aspect ProjectDelegate{

    syn lazy Session Global.projectDelegate(String p, String q){
        return null;
    }


    syn Session CommunicationChoices.projectDelegate(String p, String q){

        if(getNumCommunications()==1){
            return getCommunications(0).projectDelegate(p, q);
        }else{
            List<SessionSend> sends = new List();
            List<Session> receives = new List();
            for(Communication c : getCommunicationss()){
                Session projected = c.projectDelegate(p,q);
                if(projected.isSubtypeOf(new ExternalChoice())
                    || projected.isSubtypeOf(new SessionReceive())){
                    receives.add(projected);
                }else if(projected.isSubtypeOf(new SessionSend())){
                    sends.add((SessionSend) projected);
                }else{
                    return null;
                }
            }

            if(sends.getNumChild()>0){
                return new InternalChoice(sends);
            }

            if(receives.getNumChild()>0){
                List<SessionReceive> lis = Session.merge(receives);
                if(lis.getNumChild()==1) return lis.getChild(0);
                else{
                    ExternalChoice ext = new ExternalChoice(lis);
                    if(ext.isWellFormed()){
                        return ext;
                    }
                }
            }
        }

        return null;

    }

    syn Session Communication.projectDelegate(String p, String q){
        if(p.equals(getMessage().getSender()) && !q.equals(getMessage().getRecipient())){
            if(getMessage().getClass().getName().equals(Connecting.class.getName())){
              return new ConnectingSend(new Atom(getMessage().getRecipient()),
                                           new Atom(getMessage().getLabel()),
                                           getMessage().getTypess(), getNext().projectDelegate(p,q));
            }else{
              return new SessionSend(new Atom(getMessage().getRecipient()),
                                    new Atom(getMessage().getLabel()),
                                    getMessage().getTypess(), getNext().projectDelegate(p, q));
            }
        }else if(p.equals(getMessage().getRecipient()) && !q.equals(getMessage().getSender())){
             if(getMessage().getClass().getName().equals(Connecting.class.getName())){
                return new ConnectingReceive(new Atom(getMessage().getSender()),
                                  new Atom(getMessage().getLabel()),
                                  getMessage().getTypess(),getNext().projectDelegate(p,q));
             } else{
                return new SessionReceive(new Atom(getMessage().getSender()),
                                new Atom(getMessage().getLabel()),
                                getMessage().getTypess(),getNext().projectDelegate(p,q));
            }
        }else if(!q.equals(getMessage().getSender()) && !q.equals(getMessage().getSender()) &&
            !p.equals(getMessage().getRecipient()) && !p.equals(getMessage().getSender())){
            return getNext().projectDelegate(p,q);
        }

        return null;

    }

    syn Session Terminal.projectDelegate(String p, String q){
        return null;
    }

    syn Session StartDelegation.projectDelegate(String p, String q){
        if(!p.equals(getDelegate()) && !p.equals(getDelegating()) &&
                !q.equals(getDelegate()) && !q.equals(getDelegating())){
            return getNext().projectDelegate(p,q);
        }
        return null;
    }

    syn Session EndDelegation.projectDelegate(String p, String q){
        if(!p.equals(getDelegate()) && !p.equals(getDelegating()) &&
                !q.equals(getDelegate()) && !q.equals(getDelegating())){
            return getNext().projectDelegate(p,q);
        }else{
            if(p.equals(getDelegating()) && q.equals(getDelegate())){
                return new AcceptBackwardDelegation(new Atom(p),getNext().project(q));
            }
        }
        return null;
    }





}